entrypoints Program ;

Program. Program ::= [Def] ;
terminator nonempty Def "." ;

TDef. Def ::= ".decl" LIdent "(" [TypeArg] "|" [TypeArg] ")" ;
TALit. TypeArg ::= UIdent ;
TAGen. TypeArg ::= LIdent ;
TAList. TypeArg ::= "[" TypeArg "]" ;
TATup. TypeArg ::= "(" [TypeArg] ")" ;
TAApp. TypeArg ::= TypeArg "(" [TypeArg] ")" ;
separator TypeArg "," ;

DFact. Def ::= DeclHeader ;
DRule. Def ::= DeclHeader ":-" [Stmt] ;

DHeader. DeclHeader ::= LIdent "(" [Term] ")" ;

STrue. Stmt ::= "True" ;
SFalse. Stmt ::= "False" ;
SCall. Stmt ::= LIdent "(" [Term] ")" ;
SAss. Stmt ::= UIdent "=" Term ;
SIs. Stmt ::= UIdent "is" IExp ;
SRel. Stmt ::= IExp RelOp IExp ;
SMod. Stmt ::= Modifier "(" [Term] ")" ":" LIdent "(" [Term] ")" ;

MExt. Modifier ::= "ext" ;
MCollect. Modifier ::= "collect"; 
MOnce. Modifier ::= "once" ;

IEVar. IExp3 ::= UIdent ;
IELit. IExp3 ::= Integer ;
IENeg. IExp2 ::= "-" IExp3 ;
IEMul. IExp1 ::= IExp1 MulOp IExp2 ;
IEAdd. IExp ::= IExp AddOp IExp1 ;
coercions  IExp 3 ;

Plus.      AddOp ::= "+" ;
Minus.     AddOp ::= "-" ;
Times.     MulOp ::= "*" ;
Div.       MulOp ::= "/" ;
Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;
LE.        RelOp ::= "<=" ;
GTH.       RelOp ::= ">" ;
GE.        RelOp ::= ">=" ;
EQU.       RelOp ::= "==" ;
NE.        RelOp ::= "!=" ;

TStr. Term ::= String ;
TInt. Term ::= Integer ;
TVar. Term ::= UIdent ;
TIgnore. Term ::= "_" ;
TList. Term ::= "[" [Term] "]";
TCons. Term ::= "(" Term ":" Term ")" ;
TTup. Term ::= "(" [Term] ")" ;
TConstr. Term ::= UIdent "(" [Term] ")" ;

separator Term "," ;
separator Stmt "," ;

token UIdent (upper (letter | digit | '_')*) ;
token LIdent (lower (letter | digit | '_')*) ;
comment "%" ;
